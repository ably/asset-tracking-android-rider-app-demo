plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'io.gitlab.arturbosch.detekt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id "org.jlleitschuh.gradle.ktlint"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.ably.tracking.demo.publisher"
        minSdk 24
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField("String", "AUTHORIZATION_HEADER_BASE_64", "\"${findProperty("AUTHORIZATION_HEADER_BASE_64")}\"")
        buildConfigField("String", "AUTHORIZATION_USERNAME", "\"${findProperty("AUTHORIZATION_USERNAME")}\"")
        buildConfigField("String", "FIREBASE_REGION", "\"${findProperty("FIREBASE_REGION")}\"")
        buildConfigField("String", "FIREBASE_PROJECT_NAME", "\"${findProperty("FIREBASE_PROJECT_NAME")}\"")
    }

    signingConfigs {
        release {//for local usage only
            storeFile file('release_keystore.jks')
            keyAlias findProperty("SIGNING_KEY_ALIAS")
            keyPassword findProperty("SIGNING_KEY_PASSWORD")
            storePassword findProperty("SIGNING_STORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

detekt {
    buildUponDefaultConfig = true
    config = files("$rootDir/detekt.yml")
}

// https://github.com/jlleitschuh/ktlint-gradle#configuration
ktlint {
    verbose = true // shows rule name that failed
    android = true
    outputColorName = 'RED' // easier to spot failures in console output
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.activity:activity-compose:1.4.0'
    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.compose.material:material:1.1.1'
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation 'com.google.firebase:firebase-crashlytics'

    // AAT
    implementation "com.ably.tracking:publishing-sdk:1.1.0-rc.7"

    // Koin for Android
    implementation "io.insert-koin:koin-android:3.2.0"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation("com.squareup.okhttp3:logging-interceptor:4.10.0")

    //GSON
    implementation 'com.google.code.gson:gson:2.9.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1"
    testImplementation "com.google.truth:truth:1.1.3"
}
